plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'de.feelix.leviathan'
version = '1.2.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly 'org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.13.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(
                version: project.version
        )
    }
}

// Configure the normal jar name (not used as final artifact)
jar {
    archiveBaseName.set('Leviathan')
}

// Configure the shaded jar with relocation of fr.mrmicky.* -> de.feelix.leviathan.mrmicky.*
shadowJar {
    archiveBaseName.set('Leviathan')
    archiveClassifier.set("") // produce Leviathan-<version>.jar without classifier
    relocate 'fr.mrmicky', 'de.feelix.leviathan.mrmicky'
}

// Make the build task produce the shaded jar by default
build.dependsOn shadowJar

publishing {
    def localGradleProps = new Properties()
    def localGradlePropsFile = file('.gradle/gradle.properties')
    if (localGradlePropsFile.exists()) {
        localGradlePropsFile.withInputStream { localGradleProps.load(it) }
    }
    def repoUsername = (findProperty('user') ?: localGradleProps.getProperty('user') ?: System.getenv('REPO_USER'))
    def repoPassword = (findProperty('password') ?: localGradleProps.getProperty('password') ?: System.getenv('REPO_PASSWORD'))

    publications {
        mavenJava(MavenPublication) {
            project.shadow.component(it)
            artifact(tasks.sourcesJar)
            artifact(tasks.javadocJar)
        }
    }
    repositories {
        maven {
            name = 'releases'
            url = uri('https://repo.squarecode.de/releases')
            credentials {
                username = repoUsername
                password = repoPassword
            }
        }
    }
}

test {
    useJUnitPlatform()
}
